#!/bin/tcsh -f

#
# test_mris_expand
#
# execute mris_expand and check results against expected results
#
# Original Author: Nick Schmansky
# CVS Revision Info:
#    $Author: zkaufman $
#    $Date: 2012/12/07 16:56:17 $
#    $Revision: 1.6.4.3 $
#
# Copyright Â© 2011 The General Hospital Corporation (Boston, MA) "MGH"
#
# Terms and conditions for use, reproduction, distribution and contribution
# are found in the 'FreeSurfer Software License Agreement' contained
# in the file 'LICENSE' found in the FreeSurfer distribution, and here:
#
# https://surfer.nmr.mgh.harvard.edu/fswiki/FreeSurferSoftwareLicense
#
# Reporting: freesurfer@nmr.mgh.harvard.edu
#
# General inquiries: freesurfer@nmr.mgh.harvard.edu
#

set VERSION='$Id: test_mris_expand,v 1.6.4.3 2012/12/07 16:56:17 zkaufman Exp $'

umask 002

# backdoor bypass:
if ( $?SKIP_MRIS_EXPAND_TEST ) then
  echo "skipping test_mris_expand"
  exit 77
endif

setenv SUBJECTS_DIR $PWD
setenv FREESURFER_HOME ../distribution
#
# extract testing data
# subject is bert
#
gunzip -c testdata.tar.gz | tar xvf -

#
# run mris_expand
#

set cmd=(./mris_expand -v 111706 -thickness lh.white 0.5 lh.midgray)
echo ""
echo $cmd
echo $cmd >& test_mris_expand.log
echo ""
$cmd >>& test_mris_expand.log
if ($status != 0) then
  echo "mris_expand FAILED"
  exit 1
endif

#
# compare expected results with actual (produced) results
#

set REF_SURF=(lh.expected-midgray)
# PowerPC gives different results
if ("`uname -p`" == "powerpc") set REF_SURF=(lh.expected-midgray-ppc)
set TST_SURF=(lh.midgray)
set cmd=(../mris_diff/mris_diff --debug \
          --thresh 0.5 --no-check-nxyz \
          $REF_SURF $TST_SURF);
echo ""
echo $cmd
echo $cmd >>& test_mris_expand.log
$cmd >>& test_mris_expand.log
set diff_status=$status
if ($diff_status != 0) then
  echo "$cmd FAILED (exit status=$diff_status)"
  exit 1
endif

#
# cleanup
#
end:
rm -f lh.*

echo ""
echo "test_mris_expand passed all tests"
exit 0
